plugins {
	id 'org.springframework.boot' version '2.6.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id "jacoco"
	id "org.sonarqube"
	id "com.google.cloud.tools.jib"
}

group = 'com.searchservice'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

apply from: "gradle/docker.gradle"
apply from: "gradle/sonar.gradle"

// gradle clean
apply plugin: "eclipse"
apply plugin: 'java'


configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

//===============TO RUN JUINT TESTCASES====================
test {
    useJUnitPlatform()
    exclude "**/*IT*", "**/*IntTest*"
    testLogging {
        events 'FAILED', 'SKIPPED'
    }
    jvmArgs += '-Djava.security.egd=file:/dev/./urandom -Xmx256m'
    // uncomment if the tests reports are not generated
    // see https://github.com/jhipster/generator-jhipster/pull/2771 and https://github.com/jhipster/generator-jhipster/pull/4484
    // ignoreFailures true
    reports.html.enabled = false
}
task integrationTest(type: Test) {
	useJUnitPlatform()
	description = "Execute integration tests."
	group = "verification"
	include "**/*IT*", "**/*IntTest*"
	testLogging {
		events 'FAILED', 'SKIPPED'
	}
	jvmArgs += '-Djava.security.egd=file:/dev/./urandom -Xmx256m'
	if (project.hasProperty('testcontainers')) {
		environment 'spring.profiles.active', 'testcontainers'
	}

	// uncomment if the tests reports are not generated
	// see https://github.com/jhipster/generator-jhipster/pull/2771 and https://github.com/jhipster/generator-jhipster/pull/4484
	// ignoreFailures true
	reports.html.enabled = false
}

check.dependsOn integrationTest
task testReport(type: TestReport) {
	destinationDir = file("$buildDir/reports/tests")
	reportOn test
}

task integrationTestReport(type: TestReport) {
	destinationDir = file("$buildDir/reports/tests")
	reportOn integrationTest
}
jacocoTestReport {
    getExecutionData().setFrom(fileTree(buildDir).include("/jacoco/*.exec"))
}
//==========================================================

dependencies {

	implementation 'org.springframework.boot:spring-boot-starter-web'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	annotationProcessor "org.glassfish.jaxb:jaxb-runtime:${jaxbRuntimeVersion}"

	//Swagger UI
	implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.5.10'

	//SolrJ
	implementation group: 'org.springframework.data', name: 'spring-data-solr', version: '4.3.14'
	
	// https://mvnrepository.com/artifact/joda-time/joda-time
	implementation group: 'joda-time', name: 'joda-time', version: '2.10.13'
	

	//Lombok
	compileOnly 'org.projectlombok:lombok:1.18.22'
	annotationProcessor 'org.projectlombok:lombok:1.18.22'
	testCompileOnly 'org.projectlombok:lombok:1.18.22'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

	//JSON
	implementation group: 'org.json', name: 'json', version: '20200518'

	//RabbitMQ
	implementation "org.springframework.boot:spring-boot-starter-amqp"
	testImplementation group: 'org.springframework.amqp', name: 'spring-rabbit-test', version: '1.6.0.RELEASE'

	// Rate Limiter
	// https://mvnrepository.com/artifact/io.github.resilience4j/resilience4j-spring-boot2
	implementation group: 'io.github.resilience4j', name: 'resilience4j-spring-boot2', version: '1.5.0'
	// https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-aop
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: '2.5.4'
	// https://mvnrepository.com/artifact/com.squareup.okhttp/okhttp
	implementation group: 'com.squareup.okhttp', name: 'okhttp', version: '2.7.5'
	
	//Keycloak
	implementation group: 'org.keycloak', name: 'keycloak-spring-boot-starter', version: '11.0.2'
	
	// Spring Security
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: '2.6.3'
	
}

tasks.named('test') {
	useJUnitPlatform()
}